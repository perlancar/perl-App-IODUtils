#!perl

# DATE
# VERSION

use 5.010;
use strict;
use warnings;
no warnings 'once';

use App::IODUtils;
use Perinci::CmdLine::Any;

our %SPEC;
$SPEC{delete_iod_section} = {
    v => 1.1,
    summary => 'Delete a section (and its contents) from '.
        'IOD configuration file',
    args => {
        %App::IODUtils::common_args,
        %App::IODUtils::inplace_arg,
        section => {
            summary => 'Section name',
            schema  => 'str*',
            pos     => 1,
        },
        section_pattern => {
            summary => 'Regex to match section name',
            schema => 'str*',
        },
        all => {
            summary => 'Delete all occurrences instead of just the first',
            schema => [bool => is=>1],
        },
    },
    args_rels => {
        req_one => [qw/section section_pattern/],
    },
};
sub delete_iod_section {
    my %args = @_;
    App::IODUtils::_check_inplace(\%args);

    my $parser = App::IODUtils::_get_parser(\%args);

    my $doc = $parser->read_string($args{iod});

    my @sections;
    if (defined $args{section}) {
        @sections = ($args{section});
    } else {
        my $re = qr/$args{section_pattern}/;
        @sections = grep { /$re/ } $doc->list_sections(unique => 1);
    }
    for (@sections) {
        $doc->delete_section(
            {
                all     => $args{all},
            },
            $_,
        );
    }

    App::IODUtils::_return_mod_result(\%args, $doc);
}

Perinci::CmdLine::Any->new(
    url => '/main/delete_iod_section',
)->run;

# ABSTRACT:
# PODNAME:

=head1 SYNOPSIS

Basic usage:

 % delete-iod-section /path/to/file.iod section
